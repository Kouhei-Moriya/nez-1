/**
 * Email Demo File
 */

FILE
	= EMPTY
	/ CHAR
	/ ANY
	/ BYTE
	/ CHARSET
	/ BYTESET
	/ STRING
	/ NONTERMINAL
	/ SEQUENCE
	/ CHOICE
	/ OPTION
	/ ZEROMORE
	/ ONEMORE
	/ AND
	/ NOT

EMPTY = ''
CHAR = 'a'
ANY  = .
BYTE = 0x61  /* a */
CHARSET = [0-9]
BYTESET = 0xxxxxxx 
STRING  = 'abc'
NONTERMINAL = SEQUENCE
SEQUENCE = 'a' [0-9]
CHOICE   = 'abc' / 'a' / 'ab'

OPTION   = STRING? CHAR
ZEROMORE = 'ab'* / 'a'
ONEMORE  = 'ab'+ / 'a'
AND      = &STRING 'abc'
NOT      = !STRING SEQUENCE

example EMPTY ~c70ff aa
example CHAR ~cca2658 aa
example ANY  ~cca2658 aa
example BYTE ~cca2658 aa  
example CHARSET ~adcafb 0
example BYTESET ~adcafb 0
example STRING ~8caf11 abcd
example NONTERMINAL ~fa7122 a9
example SEQUENCE ~fa7122 a9
example CHOICE ~cca2658 ab
example OPTION ~cca2658 aa
example ZEROMORE ~1d59a2f abababa
example ONEMORE  ~cca265 aababab
example AND ~8caf11 abcd
example NOT ~fa7122 a9

/* Conditional Parsing */

ON	    = IF <on A IF> <on !A IF>

IF      = <if  A> 'a'
	    / <if !A> 'b'

example IF ~615076 b
example ON ~ba069a bab

/* Numeric Repetition */

SCAN    = <scan [0-9]> <repeat 'a'>

example SCAN ~2e3e6a 3aaa

/* Bigger Sample */
	
example EMAIL ~f3d141 mail@address.com

public EMAIL
        = LOCAL '@' DOMAIN
LOCAL
        = ([A-Za-z0-9] / '-')+ ('.' LOCAL)?
DOMAIN
        = SUBDOMAIN ('.' SUBDOMAIN)+
SUBDOMAIN
        = ([A-Za-z0-9] / '-')+

S
    = [ \t]
    
_
	= S*


/* Tree, Tree Operation */

NAME = [A-Za-z]+

NewTree = { NAME #Name }

example NewTree ~fdb051  Apple2

NoTag = { NAME }
Replace = { NAME #Name `Apple` }

example NoTag ~c5f2a69 Banana
example Replace ~fdb051 Banana

/* List,LeftPair,RightPair */

List
        = { $(Term) (',' S* $(Term))+ #List } / Term

LeftPair
        = Term {$left ',' S* $right(Term) #Pair }*

RightPair
        = { $left(Term) ',' S* $right(RightPair) #Pair } / Term

Term
        = { [A-Za-z0-9]+ #Term } S*

example List ~070f0a80 A,B,C,D

example LeftPair ~8b91eca A,B,C,D

example RightPair ~dec0e50 A,B,C,D


/* Mathematical */        

Math
	= Product {$left ( '+' #Add / '-' #Sub ) $right(Product) }*

Product
	= Value {$left ( '*' #Mul / '/' #Div / '%' #Mod ) $right(Value) }*

Value
	= { [0-9]+ #Int } _
	/ { [A-Za-z0-9_]+ #Variable } _
	/ '(' Math ')'

example Math ~6e97d13 1+2*3
example Math ~d0e1fdc 1*2+3 
example Math ~82ef86b 1+2-3 
example Math ~870539  1+2*3-4/5
example Math ~870539  1+(2*3)-(4/5)

